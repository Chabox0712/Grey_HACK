if(params.len<1)then exit("Command: decipher [hash]")

crypto = include_lib("/lib/crypto.so")
if not crypto then exit("crypto.so not found.")

obj = {}
obj.shell = get_shell()
obj.computer = obj.shell.host_computer

filePath = obj.computer.File(params[0])

getPasswords = function(lines)
	output = ""
	for line in lines
		if(not line)then continue
		if(line=="")then continue
		hashList = line.split(":")
		if(hashList.len==1)then
			output = output+"\n"+"Error"+" "+line+" "+"Hashing-Not-Supported"
			continue
		end if
		if(hashList[1].len != 32)then
			output = output+"\n"+hashList[0]+" "+hashList[1]+" "+"Invalid-Hash"
			continue
		end if
		pass = crypto.decipher(hashList[1])
		output = output+"\n"+hashList[0]+" "+hashList[1]+" "+pass
	end for
	return (output)
end function

if(typeof(filePath)=="file")then
	if(filePath.is_binary)then exit("decipher: can't read " + filePath.path + ". Binary file")
	if(not filePath.has_permission("r"))then exit("decipher: can't read file. Permission denied "+filePath.permissions)
	if(filePath.get_content.len == 0)then exit("decipher: no users found.")
	hashList = filePath.get_content.split("\n")
	output = getPasswords(hashList)
else
	hashList = params[0]
	output = getPasswords(hashList.split("\n"))
end if

print(format_columns(output))
print("\nDone.")